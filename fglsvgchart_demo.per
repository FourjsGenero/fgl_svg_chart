LAYOUT
GRID
{
Chart type: [ct                 ]  Datasets: [ds       ]
Min/Max pos:[minpos   :maxpos   ]  Grid steps X:[gsx   ]
Min/Max val:[minval   :maxval   ]  Grid steps Y:[gsy   ]
                           Skip grid labels: [sgl      ]
            [s_pts:s_pst:s_leg:s_org]
<GROUP g1                                              >
 Dataset: [c_d      ] Item: [c_i     ] [cl     ]
 Position:[c_p      ] Value:[c_v     ] [sv     ]
 Label:   [c_l                       ] [rd     ]
<                                                      >
[cv                                                    ]
[                                                      ]
[                                                      ]
[                                                      ]
[                                                      ]
[                                                      ]
[                                                      ]
[                                                      ]
Selected:[es                                           ]
}
END
END

ATTRIBUTES

COMBOBOX ct=FORMONLY.chart_type, NOT NULL,
   ITEMS=(
          (1, "Bars"),
          (2, "Points"),
          (3, "Lines"),
          (4, "Spline")
         );

SPINEDIT ds=FORMONLY.ds_count,
   VALUEMIN=1, VALUEMAX=4;

SPINEDIT minpos=FORMONLY.minpos,
   VALUEMIN=-5000, VALUEMAX=5000, STEP=10;
SPINEDIT maxpos=FORMONLY.maxpos,
   VALUEMIN=-5000, VALUEMAX=5000, STEP=10;
SPINEDIT gsx=FORMONLY.grid_sx,
   VALUEMIN=0, VALUEMAX=100;

SPINEDIT minval=FORMONLY.minval,
   VALUEMIN=-5000, VALUEMAX=5000, STEP=10;
SPINEDIT maxval=FORMONLY.maxval,
   VALUEMIN=-5000, VALUEMAX=5000, STEP=10;
SPINEDIT gsy=FORMONLY.grid_sy,
   VALUEMIN=0, VALUEMAX=100;

SPINEDIT sgl=FORMONLY.skip_gl,
   VALUEMIN=1, VALUEMAX=10;

CHECKBOX s_pts = FORMONLY.show_points, NOT NULL,
   TEXT="Show points";
CHECKBOX s_pst = FORMONLY.points_style, NOT NULL,
   TEXT="Pts style";
CHECKBOX s_leg = FORMONLY.show_legend, NOT NULL,
   TEXT="Show legend";
CHECKBOX s_org = FORMONLY.show_origin, NOT NULL,
   TEXT="Show origin";

GROUP g1: group1, TEXT="Values";

SPINEDIT c_d= FORMONLY.curr_dataset, NOT NULL;
SPINEDIT c_i= FORMONLY.curr_item, NOT NULL;
EDIT c_p= FORMONLY.curr_position, NOT NULL;
EDIT c_v= FORMONLY.curr_value, NOT NULL;
EDIT c_l= FORMONLY.curr_label, NOT NULL;
BUTTON sv: set_value, TEXT="Set";
BUTTON cl: clear, TEXT="Clear";
BUTTON rd: random, TEXT="Random";

WEBCOMPONENT cv=FORMONLY.canvas,
   COMPONENTTYPE="fglsvgcanvas",
   PROPERTIES=(selection="svg_selection"),
   SIZEPOLICY=FIXED,
   STRETCH=BOTH,
   SCROLLBARS=NONE
;

EDIT es=FORMONLY.elem_selected, SCROLL;

END
